plugins {
    id 'java'
    id 'jacoco'
}

group 'org.java-junit5-mockito-examples'
version '0.1'

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
    }
    test {
        java {
            srcDirs = ['test']
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.9.0"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.9.0"

    testImplementation 'org.mockito:mockito-core:4.7.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.7.0'

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.9.0"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = true
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}

jacoco {
    toolVersion = "0.8.8"
    reportsDirectory = layout.buildDirectory.dir('codecoverage')
}

task fullTest(type: Test) {
    testLogging {
        events "passed", "skipped", "failed"
    }
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

task fastTest(type: Test) {
    testLogging {
        events "passed", "skipped", "failed"
    }
    useJUnitPlatform {
        includeTags "fast"
    }
    finalizedBy jacocoTestReport
}